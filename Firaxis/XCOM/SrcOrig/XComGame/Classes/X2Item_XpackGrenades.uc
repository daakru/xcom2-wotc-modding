class X2Item_XpackGrenades extends X2Item config(GameData_WeaponData);

//***** Grenade Variables *****
//*** Config var declarations

var config WeaponDamageValue MOUNTAINMIST_GRENADE_BASEDAMAGE;
var config int MOUNTAINMIST_GRENADE_SOUNDRANGE;
var config int MOUNTAINMIST_GRENADE_ENVIRONMENTDAMAGE;
var config int MOUNTAINMIST_GRENADE_CLIPSIZE;
var config int MOUNTAINMIST_GRENADE_RANGE;
var config int MOUNTAINMIST_GRENADE_RADIUS;
var config int MOUNTAINMIST_NUM_TURNS;
var config float MOUNTAINMIST_SIGHTRADIUS_POSTMULTAMOUNT;

var config int CONCUSSION_GRENADE_ISOUNDRANGE;
var config int CONCUSSION_GRENADE_IENVIRONMENTDAMAGE;
var config int CONCUSSION_GRENADE_ICLIPSIZE;
var config int CONCUSSION_GRENADE_RANGE;
var config int CONCUSSION_GRENADE_RADIUS;
var config int CONCUSSION_GRENADE_DISORIENT_APPLY_CHANCE_PERCENT;
var config int CONCUSSION_GRENADE_DAZE_TURNS;

static function array<X2DataTemplate> CreateTemplates()
{
	local array<X2DataTemplate> Grenades;

	Grenades.AddItem(CreateMountainMistGrenade());
	Grenades.AddItem(CreateHunterConcussionGrenade());

	return Grenades;
}

static function X2DataTemplate CreateMountainMistGrenade()
{
	local X2GrenadeTemplate Template;

	`CREATE_X2TEMPLATE(class'X2GrenadeTemplate', Template, 'MountainMistGrenade');

	Template.strImage = "img:///UILibrary_StrategyImages.InventoryIcons.Inv_AlienGrenade";
	Template.EquipSound = "StrategyUI_Grenade_Equip";

	Template.Abilities.AddItem('ThrowGrenade');
	Template.bFriendlyFire = false;

	Template.iRange = default.MOUNTAINMIST_GRENADE_RANGE;
	Template.iRadius = default.MOUNTAINMIST_GRENADE_RADIUS;
	Template.BaseDamage = default.MOUNTAINMIST_GRENADE_BASEDAMAGE;
	Template.iEnvironmentDamage = default.MOUNTAINMIST_GRENADE_ENVIRONMENTDAMAGE;
	Template.iClipSize = default.MOUNTAINMIST_GRENADE_CLIPSIZE;
	Template.iSoundRange = default.MOUNTAINMIST_GRENADE_SOUNDRANGE;

	Template.ThrownGrenadeEffects.AddItem(class'X2Effect_Blind'.static.CreateBlindEffect(default.MOUNTAINMIST_NUM_TURNS, default.MOUNTAINMIST_SIGHTRADIUS_POSTMULTAMOUNT));
	
//BEGIN AUTOGENERATED CODE: Template Overrides 'MountainMistGrenade'
	Template.GameArchetype = "WP_Grenade_MountainMist.WP_Grenade_MountainMist";
//END AUTOGENERATED CODE: Template Overrides 'MountainMistGrenade'

	Template.CanBeBuilt = false;

	return Template;
}

static function X2DataTemplate CreateHunterConcussionGrenade()
{
	local X2GrenadeTemplate Template;
	local X2Effect_ApplyWeaponDamage WeaponDamageEffect;
	local X2Effect_Knockback KnockbackEffect;
	local X2Effect_Dazed DazeEffect;

	`CREATE_X2TEMPLATE(class'X2GrenadeTemplate', Template, 'HunterConcussionGrenade');

	Template.strImage = "img:///UILibrary_StrategyImages.X2InventoryIcons..Inv_Concussion_Grenade";
	Template.EquipSound = "StrategyUI_Grenade_Equip";
	Template.AddAbilityIconOverride('ThrowGrenade', "img:///UILibrary_PerkIcons.UIPerk_grenade_flash");
	Template.iRange = default.CONCUSSION_GRENADE_RANGE;
	Template.iRadius = default.CONCUSSION_GRENADE_RADIUS;

	Template.bFriendlyFire = false;
	Template.bFriendlyFireWarning = false;
	Template.Abilities.AddItem('ThrowGrenade');

	//We need to have an ApplyWeaponDamage for visualization, even if the grenade does 0 damage (makes the unit flinch, shows overwatch removal)
	WeaponDamageEffect = new class'X2Effect_ApplyWeaponDamage';
	WeaponDamageEffect.bExplosiveDamage = true;
	Template.ThrownGrenadeEffects.AddItem(WeaponDamageEffect);

	KnockbackEffect = new class'X2Effect_Knockback';
	KnockbackEffect.KnockbackDistance = 2;
	KnockbackEffect.OnlyOnDeath = false;
	Template.ThrownGrenadeEffects.AddItem(KnockbackEffect);

	DazeEffect = class'X2StatusEffects_XPack'.static.CreateDazedStatusEffect(default.CONCUSSION_GRENADE_DAZE_TURNS, 100);
	DazeEffect.bRemoveWhenSourceDies = true;
	Template.ThrownGrenadeEffects.AddItem(DazeEffect);

	Template.LaunchedGrenadeEffects = Template.ThrownGrenadeEffects;

//BEGIN AUTOGENERATED CODE: Template Overrides 'HunterConcussionGrenade'
	Template.GameArchetype = "WP_Concussion_Grenade.WP_Concussion_Grenade";
//END AUTOGENERATED CODE: Template Overrides 'HunterConcussionGrenade'

	Template.CanBeBuilt = false;

	Template.iSoundRange = default.CONCUSSION_GRENADE_ISOUNDRANGE;
	Template.iEnvironmentDamage = default.CONCUSSION_GRENADE_IENVIRONMENTDAMAGE;
	Template.iClipSize = default.CONCUSSION_GRENADE_ICLIPSIZE;
	Template.Tier = 0;

	Template.SetUIStatMarkup(class'XLocalizedData'.default.RangeLabel, , default.CONCUSSION_GRENADE_RANGE);
	Template.SetUIStatMarkup(class'XLocalizedData'.default.RadiusLabel, , default.CONCUSSION_GRENADE_RADIUS);

	return Template;
}

defaultproperties
{
	bShouldCreateDifficultyVariants = true
}
